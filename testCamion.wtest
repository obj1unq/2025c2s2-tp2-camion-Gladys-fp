import camion.*
import cosas.*
import transporte.*

describe "Camión cargando y descargando vehículos" {
  test "Carga y descarga del camion" {
    // Carga inicial de bumblebee
    camion.cargar(bumblebee) 
    assert.equals(#{bumblebee}, camion.cosas())
    // Carga adicional de Knight Rider
    camion.cargar(knightRider)
    assert.equals(#{bumblebee, knightRider}, camion.cosas())
    // Descarga de bumblebee
    camion.descargar(bumblebee)
    assert.equals(#{knightRider}, camion.cosas())
    // Intento de carga duplicada de Knight Rider
    assert.throwsException({camion.cargar(knightRider)})
    // Intento de descarga de bumblebee cuando no está
    assert.throwsException({camion.descargar(bumblebee)})
    
  }
}

describe "Validación de pesos en el camión"{

  test "Todo peso par" {
    arenaAGranel.peso(50)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)
    assert.that(camion.todoPesoEsPar())
  }
  test "Todo peso es impar"{  
    arenaAGranel.peso(51)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)
    assert.notThat(camion.todoPesoEsPar())
  }

  test "Hay alguno que pesa" {
    arenaAGranel.peso(50) 
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)
    assert.that(camion.tieneCosaConPeso(50))
    assert.that(camion.tieneCosaConPeso(800))
    assert.notThat(camion.tieneCosaConPeso(500))
  }

  test "Peso no excedido" {
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.notThat(camion.pesoExcedido())
  }
    test "Peso excedido" {
    paqueteDeLadrillos.cantidadDeLadrillos(400)
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.that(camion.pesoExcedido())
    
  }
}   

describe "Peligrosidad de los objetos "{
  test "Nivel de peligrosidad" {
    bumblebee.tranformarEn(auto)
    bateriaAntiaerea.misiles(descargado)
    camion.cargar(bateriaAntiaerea)
    camion.cargar(bumblebee)
    camion.cargar(residuosRadiactivos)

    assert.equals(bateriaAntiaerea, camion.cantidadDePeligrosidadDe(0))
    assert.equals(bumblebee, camion.cantidadDePeligrosidadDe(15))
    assert.equals(residuosRadiactivos, camion.cantidadDePeligrosidadDe(200))
    assert.throwsException({ => camion.cantidadDePeligrosidadDe(30) })
    bumblebee.tranformarEn(robot)
    assert.equals(bumblebee, camion.cantidadDePeligrosidadDe(30))
    bateriaAntiaerea.misiles(cargado)
    assert.equals(bateriaAntiaerea, camion.cantidadDePeligrosidadDe(100))
  }

  test "Cosas peligrosas" {
    bateriaAntiaerea.misiles(cargado)
    camion.cargar(knightRider)
    camion.cargar(bateriaAntiaerea)
    
    assert.equals(#{bateriaAntiaerea}, camion.cosasMasPeligrosasQueNivel(15))
    assert.equals(#{bateriaAntiaerea}, camion.cosasMasPeligrosasQue(bumblebee))
    assert.equals(#{knightRider, bateriaAntiaerea}, camion.cosasMasPeligrosasQueNivel(2))
    assert.equals(#{bateriaAntiaerea, knightRider}, camion.cosasMasPeligrosasQue(paqueteDeLadrillos))
    assert.equals(#{}, camion.cosasMasPeligrosasQueNivel(300))
    assert.equals(#{},camion.cosasMasPeligrosasQue(residuosRadiactivos))
  } 
}

describe "Circular en la ruta" {
  test "Puede circular con bumblebee en auto y 300 ladrillos en ruta nivel 20" {
    bumblebee.tranformarEn(auto) 
    paqueteDeLadrillos.cantidadDeLadrillos(300) 
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.that(camion.puedeCircularRutaDeNivel(20))
  }

  test "No puede circular con bumblebee en auto y 400 ladrillos en ruta nivel 20 (exceso de peso)" {
    bumblebee.tranformarEn(auto) 
    paqueteDeLadrillos.cantidadDeLadrillos(400) 
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.notThat(camion.puedeCircularRutaDeNivel(20))
  }

  test "No puede circular con bumblebee en modo robot y 300 ladrillos en ruta nivel 20 (exceso de peligrosidad)" {
    bumblebee.tranformarEn(robot) 
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.notThat(camion.puedeCircularRutaDeNivel(20))
  }
}

describe "mas Cosas"{
  test "contenedor Portuario vacio" {
    assert.equals(100, contenedorPortuario.peso())
    assert.equals(0, contenedorPortuario.nivelPeligrosidad())
  }

  test "Contenedor cargado con Knight Rider y 50 kg de arena a granel" {
    contenedorPortuario.cargarCosaAContenedor(knightRider)
    arenaAGranel.peso(50)
    contenedorPortuario.cargarCosaAContenedor(arenaAGranel)
    assert.equals(650, contenedorPortuario.peso())
    assert.equals(10, contenedorPortuario.nivelPeligrosidad())
  }
  
  test "Embalaje de seguridad para Knight Rider" {
    embalajeDeSeguridad.embalar(knightRider)
    assert.equals(500, embalajeDeSeguridad.peso())
    assert.equals(5, embalajeDeSeguridad.nivelPeligrosidad())
    
  }

  test "Embalaje de seguridad para Bumbleblee en modo auto" {
    bumblebee.tranformarEn(auto)
    embalajeDeSeguridad.embalar(bumblebee)
    assert.equals(800, embalajeDeSeguridad.peso())
    assert.equals(7.5, embalajeDeSeguridad.nivelPeligrosidad())
  }
}

describe "Cosa con con peso entre un valor maximo y minimo" {
  method initialize(){
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(bumblebee) //800
    camion.cargar(paqueteDeLadrillos) //600
  }
  test "No tiene algo que pesa entre 5 y 100" {
    assert.throwsException({ camion.cosaQuePesaEntreLosValores(5, 100) })
  }
  
  test "Tiene algo que pesa entre 5 y 1000" {
    assert.equals(true, camion.cosaQuePesaEntreLosValores(5, 1000) != null)
  }
 
  test "No tiene algo que pese entre 900 y 1000" { 
    assert.throwsException({ camion.cosaQuePesaEntreLosValores(900, 1000) })
  }
}
   
    


describe "Todos los pesos de las cosas que tiene cargado el camion" {
  test "Peso de Bumblebee y Paquete de ladrillos" {
    paqueteDeLadrillos.cantidadDeLadrillos(300)
    camion.cargar(bumblebee)
    camion.cargar(paqueteDeLadrillos)
    assert.equals([800,600], camion.pesoDeLasCosasCargadas())
  }
}

describe "Bultos que carga el camion" {
  test "Primer caso" {
    contenedorPortuario.cargarCosaAContenedor(knightRider)
    contenedorPortuario.cargarCosaAContenedor(bumblebee)
    camion.cargar(contenedorPortuario)  

    embalajeDeSeguridad.embalar(residuosRadiactivos)
    camion.cargar(embalajeDeSeguridad)

    camion.cargar(arenaAGranel)

    paqueteDeLadrillos.cantidadDeLadrillos(50)
    camion.cargar(paqueteDeLadrillos)
   
    bateriaAntiaerea.misiles(descargado)
    camion.cargar(bateriaAntiaerea)
    assert.equals(9, camion.cantidadDeBultosQueLleva()) 
  }

  test "Segundo caso" {
    paqueteDeLadrillos.cantidadDeLadrillos(150)
    camion.cargar(paqueteDeLadrillos)

    bateriaAntiaerea.misiles(cargado)
    camion.cargar(bateriaAntiaerea)   

    camion.cargar(contenedorPortuario)  
    assert.equals(5, camion.cantidadDeBultosQueLleva()) 
  }

  test "Tercer caso" {
    paqueteDeLadrillos.cantidadDeLadrillos(350)
    camion.cargar(paqueteDeLadrillos)

    camion.cargar(arenaAGranel)   

    assert.equals(4, camion.cantidadDeBultosQueLleva()) 
  }
}

describe "Accidente de camion" {
  test "Primer accidente" {
    bumblebee.tranformarEn(auto)
    contenedorPortuario.cargarCosaAContenedor(knightRider)
    contenedorPortuario.cargarCosaAContenedor(bumblebee)
    camion.cargar(contenedorPortuario)  

    residuosRadiactivos.peso(30)
    embalajeDeSeguridad.embalar(residuosRadiactivos)
    camion.cargar(embalajeDeSeguridad)

    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)

    paqueteDeLadrillos.cantidadDeLadrillos(100)
    camion.cargar(paqueteDeLadrillos)
   
    bateriaAntiaerea.misiles(cargado)
    camion.cargar(bateriaAntiaerea)

    camion.sufrirAccidente()

    assert.equals(robot, bumblebee.tranformacion()) 
    assert.equals(30, residuosRadiactivos.peso())
    assert.equals(70, arenaAGranel.peso())
    assert.equals(88, paqueteDeLadrillos.cantidadDeLadrillos())
    assert.equals(descargado, bateriaAntiaerea.misiles())
  }

  test "Segundo accidente" {
    bumblebee.tranformarEn(robot)
    camion.cargar(bumblebee)  

    residuosRadiactivos.peso(30)
    camion.cargar(residuosRadiactivos)

    arenaAGranel.peso(50)
    camion.cargar(arenaAGranel)

    paqueteDeLadrillos.cantidadDeLadrillos(10)
    camion.cargar(paqueteDeLadrillos)
   
    bateriaAntiaerea.misiles(descargado)
    camion.cargar(bateriaAntiaerea)

    camion.sufrirAccidente()

    //assert.equals("auto", bumblebee.tranformarEn11()) 
    assert.equals(45, residuosRadiactivos.peso())
    assert.equals(0, paqueteDeLadrillos.cantidadDeLadrillos())
   // assert.equals(false, bateriaAntiaerea.misiles222())
  }
}


describe "Transporte" {
  method initialize(){
    almacen.almacenarCosa(knightRider)
    almacen.almacenarCosa(residuosRadiactivos)
    bumblebee.tranformarEn(auto)
    arenaAGranel.peso(50)
    camion.cargar(bumblebee)
    camion.cargar(arenaAGranel)    
  }

  test "Ruta 9 exitoso" {
    camion.transportar(almacen, ruta9)
    assert.equals(#{knightRider,arenaAGranel,bumblebee,residuosRadiactivos}, almacen.cosasAlmacenadas())
    assert.equals(#{}, camion.cosas())
  }
 
  test "Ruta 9 fallido por peligrosidad" {
    bumblebee.tranformarEn(robot)
    assert.throwsException({camion.transportar(almacen, ruta9)})
    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosasAlmacenadas())
    assert.equals(#{bumblebee, arenaAGranel}, camion.cosas())
  }
 
  test "Ruta 9 fallido por exceso de peso" {
    arenaAGranel.peso(2000)
    assert.throwsException({camion.transportar(almacen, ruta9)})
    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosasAlmacenadas())
    assert.equals(#{bumblebee, arenaAGranel}, camion.cosas())
  }

  test "Camino vecinal exitoso" {
    caminoVecinal.pesoMaximoPermitido(2000)
    camion.transportar(almacen, caminoVecinal)
    assert.equals(#{knightRider,arenaAGranel,bumblebee,residuosRadiactivos}, almacen.cosasAlmacenadas())
    assert.equals(#{}, camion.cosas())
  }

  test "Camino vecinal fallido" {
    caminoVecinal.pesoMaximoPermitido(300)
    assert.throwsException({camion.transportar(almacen, caminoVecinal)})
    assert.equals(#{knightRider,residuosRadiactivos}, almacen.cosasAlmacenadas())
    assert.equals(#{bumblebee, arenaAGranel}, camion.cosas())
  }


}

